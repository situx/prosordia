---
layout: base
---

<script type="text/javascript">
   lexemesSparql = `
# Data for a specific lexeme
SELECT DISTINCT
  ?description
  (?valueLabel AS ?value_) ?value_Url
  ?source ?sourceUrl
WHERE {
  VALUES ?name { wd:${ q } }
  {
    BIND(1 AS ?order)
    BIND("Name" AS ?description)
    ?name rdfs:label ?value .
	FILTER(lang(?value)="en")
    BIND(?value AS ?valueLabel)
  } UNION 
  {
    BIND(1 AS ?order)
	{SELECT ?description (GROUP_CONCAT(?valueurl; separator=" // ") AS ?value_Url) (GROUP_CONCAT(?valuelabel; separator=" // ") AS ?valueLabel) WHERE {     
	  BIND("Alternative Name" AS ?description)
	  BIND(wd:${ q } AS ?valueurl)
	  wd:${ q } skos:altLabel ?value .
	  BIND(?value AS ?valuelabel)
    }GROUP BY ?description ORDER BY ?valuelabel }
  }
  UNION 
  {
    BIND(2 AS ?order)
    BIND("Instance of" AS ?description)
    ?name p:P2 ?value_statement .
    ?value_statement ps:P2 ?value .
    ?value rdfs:label ?valueLabel .
    FILTER (LANG(?valueLabel) = 'en')
    BIND(CONCAT("../property_value?p=P31&q=", SUBSTR(STR(?value), 37),"&qLabel=",?valueLabel)  AS ?value_Url)
    OPTIONAL {
      ?value_statement prov:wasDerivedFrom / pr:P248 ?source_ .
      ?source_ rdfs:label ?source .
      FILTER (LANG(?source) = 'en')
      BIND(CONCAT("../reference?q=", SUBSTR(STR(?source_), 37))  AS ?sourceUrl)
    }
  } UNION 
  {
    BIND(3 AS ?order)
	{SELECT ?description (GROUP_CONCAT(?valueurl; separator=" // ") AS ?value_Url) (GROUP_CONCAT(?valuelabel; separator=" // ") AS ?valueLabel) WHERE {     
	  BIND("Type of work" AS ?description)
	  wd:${ q } wdt:P121 ?valueurl .
	  ?valueurl rdfs:label ?valuelabel .
	  FILTER(lang(?valuelabel)="en")
    }GROUP BY ?description ORDER BY ?valuelabel }  
  } UNION 
  {
    BIND(4 AS ?order)
    BIND("Language" AS ?description)
    ?name wdt:P18 ?value .
    ?value rdfs:label ?valueLabel .
    FILTER (LANG(?valueLabel) = 'en')
    BIND(CONCAT("../language?q=", SUBSTR(STR(?value), 37),"&qLabel=",?valueLabel)  AS ?value_Url)
  }
  UNION 
  {
    BIND(5 AS ?order)
    BIND("Writing material" AS ?description)
    ?name wdt:P480 ?value_Url .
    ?value_Url rdfs:label ?valueLabel .
    FILTER(lang(?valueLabel)="en")
  }UNION 
  {
    BIND(6 AS ?order)
	{SELECT ?description (GROUP_CONCAT(?valueurl; separator=" // ") AS ?value_Url) (GROUP_CONCAT(?valuelabel; separator=" // ") AS ?valueLabel) WHERE {     
	  BIND("Findspot" AS ?description)
	  wd:${ q } wdt:P695 ?valueurl .
	  ?valueurl rdfs:label ?valuelabel .
	  FILTER(lang(?valuelabel)="en")
    }GROUP BY ?description ORDER BY ?valuelabel }  
  }UNION 
  {
    BIND(7 AS ?order)
    BIND("Online presence" AS ?description)
    ?name wdt:P138 ?value_Url .
    BIND(?value_Url AS ?valueLabel)
  }UNION 
  {
    BIND(8 AS ?order)
    BIND("Part of" AS ?description)
    ?name wdt:P8 ?value_Url .
    ?value_Url rdfs:label ?valueLabel .
    FILTER(lang(?valueLabel)="en")
  }UNION 
  {
    BIND(9 AS ?order)
    BIND("CDLI ID" AS ?description)
    ?name wdt:P692 ?value .
    BIND(?value AS ?valueLabel)
    BIND(CONCAT("https://cdli.ucla.edu/search/archival_view.php?ObjectID=",?value) AS ?value_Url)
  }UNION 
  {
    BIND(10 AS ?order)
    BIND("Online information" AS ?description)
    ?name wdt:P146 ?value_Url .
    BIND(?value_Url AS ?valueLabel)
  }UNION 
  {
    BIND(11 AS ?order)
    BIND("3d model" AS ?description)
    ?name wdt:P1034 ?value_Url .
    BIND(?value_Url AS ?valueLabel)
  }UNION 
  {
    BIND(12 AS ?order)
    BIND("Width" AS ?description)
	?name p:P60 ?width_stat .
    ?width_stat psv:P60 ?width_node .
    ?width_node wikibase:quantityAmount ?widthh .
    ?width_node wikibase:quantityUnit ?widthUnitt .
    ?widthUnitt rdfs:label ?widthUnit .
    FILTER(lang(?widthUnit)="en")
    BIND(CONCAT(STR(?widthh)," ",STR(?widthUnit)) AS ?width)
	BIND(?width AS ?value)
    BIND(?width AS ?valueLabel)
  }UNION 
  {
    BIND(13 AS ?order)
    BIND("Height" AS ?description)
	?name p:P59 ?height_stat .
    ?height_stat psv:P59 ?height_node .
    ?height_node wikibase:quantityAmount ?heighth .
    ?height_node wikibase:quantityUnit ?heightUnitt .
    ?heightUnitt rdfs:label ?heightUnit .
    FILTER(lang(?heightUnit)="en")
    BIND(CONCAT(STR(?heighth)," ",STR(?heightUnit)) AS ?height)
	BIND(?height AS ?value)
    BIND(?height AS ?valueLabel)
  }UNION 
  {
    BIND(14 AS ?order)
    BIND("Thickness" AS ?description)
	?name p:P61 ?thick_stat .
    ?thick_stat psv:P61 ?thick_node .
    ?thick_node wikibase:quantityAmount ?thickh .
    ?thick_node wikibase:quantityUnit ?thickUnitt .
    ?thickUnitt rdfs:label ?thickUnit .
    FILTER(lang(?thickUnit)="en")
    BIND(CONCAT(STR(?thickh)," ",STR(?thickUnit)) AS ?thick)
	BIND(?thick AS ?value)
    BIND(?thick AS ?valueLabel)
  }UNION 
  {
    BIND(15 AS ?order)
    BIND("Area" AS ?description)
	?name p:P1020 ?area_stat .
    ?area_stat psv:P1020 ?area_node .
    ?area_node wikibase:quantityAmount ?areah .
    ?area_node wikibase:quantityUnit ?areaUnitt .
    ?areaUnitt rdfs:label ?areaUnit .
    FILTER(lang(?areaUnit)="en")
    BIND(CONCAT(STR(?areah)," ",STR(?areaUnit)) AS ?area)
	BIND(?area AS ?value)
    BIND(?area AS ?valueLabel)
  }UNION 
  {
    BIND(16 AS ?order)
    BIND("Volume" AS ?description)
	?name p:P1091 ?volume_stat .
    ?volume_stat psv:P1091 ?volume_node .
    ?volume_node wikibase:quantityAmount ?volumeh .
    ?volume_node wikibase:quantityUnit ?volumeUnitt .
    ?volumeUnitt rdfs:label ?volumeUnit .
    FILTER(lang(?volumeUnit)="en")
    BIND(CONCAT(STR(?volumeh)," ",STR(?volumeUnit)) AS ?volume)
	BIND(?volume AS ?value)
    BIND(?volume AS ?valueLabel)
  } UNION {
    BIND(17 AS ?order)
    BIND("Present holding" AS ?description)
    ?name wdt:P329 ?value_Url .
    ?value_Url rdfs:label ?valueLabel .
    FILTER(lang(?valueLabel)="en")
  } 
}
ORDER BY ?order
`


listOfUsesSparql = `
SELECT 
  ?lexeme ?lexemeLabel
  ?form ?formLabel
  ?use
WHERE {
  VALUES ?reference { wd:${ q } }
  {
    ?lexeme p:P5831 ?use_statement . 
    ?lexeme wikibase:lemma ?lexemeLabel .
    ?use_statement ps:P5831 ?use .
    OPTIONAL {	
      ?use_statement pq:P5830 ?form .
      ?form ontolex:representation ?formLabel .
    }
    ?use_statement prov:wasDerivedFrom / pr:P248 ?reference .
  }
  UNION
  {
    ?lexeme p:P5323 ?attested_statement . 
    ?lexeme wikibase:lemma ?lexemeLabel .
    ?attested_statement ps:P5323  ?reference .
    BIND("-" AS ?use)
    BIND("-" AS ?form)
  }
}
`

nameSparql = `
SELECT ?name ?nameLabel ?gender ?genderLabel ?language ?languageLabel WHERE {
    ?name wdt:P143 wd:${ q } .
	?name rdfs:label ?nameLabel .
	FILTER(lang(?nameLabel)="en")
	OPTIONAL {
		?name wdt:P18 ?language .
		?language rdfs:label ?languageLabel .
		FILTER(lang(?languageLabel)="en")
	} 
	OPTIONAL {
		?name wdt:P625 ?gender .
		?gender rdfs:label ?genderLabel .
		FILTER(lang(?genderLabel)="en")
	} 
}
ORDER BY ?nameLabel
`

imagesSparql = `
SELECT 
  ?image ?surface ?surfaceLabel
WHERE {
    wd:${ q } p:P188 ?imagee .
	?imagee ps:P188 ?image .
	OPTIONAL {
		?imagee pq:P1029 ?surface .
		?surface rdfs:label ?surfaceLabel .
		FILTER(lang(?surfaceLabel)="en")
	}
}
`


  $(document).ready(function() {
      sparqlToDataTable(lexemesSparql, "#lexemes",
			options={
			    'linkPrefixes': {
				'lexeme': '../l',
			    }});
      sparqlToDataTable(imagesSparql, "#images",
			options={
			    'linkPrefixes': {
				'lexeme': '../l',
				'form': '../lf', 
			    }});
	  sparqlToDataTable(nameSparql, "#name",
		options={
			'linkPrefixes': {
			'name': '../n',
		}});
  });
  
</script>

<div class="reference-entry">
  
  <h1>Reference: ${ qLabel } (${ q })</h1>

  <p>
    <a title="Back"
       role="button"
       class="btn btn-outline-dark btn-sm"
       href="../reference_index/index.html">←</a>
    <a title="View and edit reference on Wikidata"
       role="button"
       class="btn btn-outline-dark btn-sm"
       href="https://database.factgrid.de/entity/${ q }">FactGrid ↗</a>
  </p>
    
  <hr>

  <h2>Reference Data</h2>

  <table class="table table-hover" id="lexemes"></table>

  <h2>Images</h2>

  <table class="table table-hover" id="images"></table>
 
  <h2>Names on medium</h2>

  <table class="table table-hover" id="name"></table> 
    
</div>



