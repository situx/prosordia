---
layout: base
---

<script type="text/javascript">
    valuesForLexemesByValueSparql = `
SELECT
  ?count
  ?value ?valueLabel
  ("→" AS ?zoom)
  (IF(STRSTARTS(STR(?value), "http://www.wikidata.org/entity/Q"), CONCAT("${ p }/value/", SUBSTR(STR(?value), 32)), "") AS ?zoomUrl)
  ?example_lexeme ?example_lexemeLabel
WHERE {
  {
    SELECT
      (COUNT(*) AS ?count) ?value
      (SAMPLE(?lexeme) AS ?example_lexeme)
    {
      ?lexeme a ontolex:LexicalEntry ; wdt:${ p } ?value .
    }
    GROUP BY ?value
  }
  OPTIONAL {
    { ?value wikibase:lemma ?valueLabel1 . }
    UNION
    { ?value rdfs:label ?valueLabel2 . FILTER (LANG(?valueLabel2) = 'en') }
    BIND(COALESCE(?valueLabel1, ?valueLabel2) AS ?valueLabel)
  }
  ?example_lexeme wikibase:lemma ?example_lexemeLabel    
}
ORDER BY DESC(?count)
LIMIT 1000
`


 valuesForLexemesByLanguageAndValueSparql = `
SELECT
  ?lexeme ?lexemeLabel
  ?language ?languageLabel
  ?value ?valueLabel
  # ("→" AS ?zoom)
  # (CONCAT("${ p }/value/", SUBSTR(STR(?value), 32)) AS ?zoomUrl)
WHERE {
  ?lexeme wikibase:lemma ?lexemeLabel .
  ?lexeme a ontolex:LexicalEntry .
  ?lexeme wdt:${ p } ?value .
  ?lexeme dct:language ?language
  OPTIONAL {
    ?language rdfs:label ?languageLabel .
    FILTER (LANG(?languageLabel) = 'en')
  }
  OPTIONAL {
    { ?value wikibase:lemma ?valueLabel1 . }
    UNION
    { ?value rdfs:label ?valueLabel2 . FILTER (LANG(?valueLabel2) = 'en') }
    BIND(COALESCE(?valueLabel1, ?valueLabel2) AS ?valueLabel)
  }
}
LIMIT 1000
  `

  formSparql = `
SELECT
  ?lexeme ?lexemeLabel
  ?form ?formLabel
  ?language ?languageLabel
  ?value ?valueLabel
  # ("→" AS ?zoom)
  # (CONCAT("${ p }/value/", SUBSTR(STR(?value), 32)) AS ?zoomUrl)
WHERE {
  ?lexeme wikibase:lemma ?lexemeLabel .
  ?lexeme ontolex:lexicalForm ?form .
  ?form ontolex:representation ?formLabel .
  ?form wdt:${ p } ?value .
  ?lexeme dct:language ?language
  OPTIONAL {
    ?language rdfs:label ?languageLabel .
    FILTER (LANG(?languageLabel) = 'en')
  }
  OPTIONAL {
    { ?value wikibase:lemma ?valueLabel1 . }
    UNION
    { ?value rdfs:label ?valueLabel2 . FILTER (LANG(?valueLabel2) = 'en') }
    BIND(COALESCE(?valueLabel1, ?valueLabel2) AS ?valueLabel)
  }
}
LIMIT 10000
`


  senseSparql = `
SELECT
  ?lexeme ?lexemeLabel
  ?sense ?senseUrl
  ?language ?languageLabel
  ?value ?valueUrl
WHERE {
  ?lexeme wikibase:lemma ?lexemeLabel .
  ?lexeme ontolex:sense ?sense_ .
  BIND(SUBSTR(STR(?sense_), 32) AS ?sense)
  BIND(CONCAT("../", ?sense) AS ?senseUrl)

  ?sense_ wdt:${ p } ?value_ .
  BIND(SUBSTR(STR(?value_), 32) AS ?value)
  BIND(CONCAT("../", ?value) AS ?valueUrl)
  ?lexeme dct:language ?language
  OPTIONAL {
    ?language rdfs:label ?languageLabel .
    FILTER (LANG(?languageLabel) = 'en')
  }
}
LIMIT 10000
`




 $(document).ready(function() {
     sparqlToDataTable(valuesForLexemesByValueSparql, "#values-for-lexemes-by-value", {
	 linkPrefixes: {
	     example_lexeme: '../l',
	     value: '../property_value'
	 }});
     sparqlToDataTable(valuesForLexemesByLanguageAndValueSparql, "#values-for-lexemes-by-language-and-value", {
	 linkPrefixes: {
	     lexeme: '../l',
	     language: '../language', 
	     value: '../property_value'}});
     sparqlToDataTable(formSparql, "#forms", {
	 linkPrefixes: {
	     lexeme: '../l',
	     form: '../lf',
	     language: '../language', 
	     value: '../property_value'}});
     sparqlToDataTable(senseSparql, "#senses", {
	 linkPrefixes: {
	     lexeme: '../l',
	     form: '../lf',
	     language: '../language', 
	     value: '../property_value'}});
 });


</script>

<div class="property-entries">
  <h1>Property: ${ p }</h1>

  <a title="Back"
     role="button"
     class="btn btn-outline-dark btn-sm"
     href="../property_index/index.html">←</a>
  <a title="View and edit property on Wikidata"
     role="button"
     class="btn btn-outline-dark btn-sm"
     href="https://www.wikidata.org/wiki/Property:${ p }">Wikidata ↗</a>
  <a title="View property in Scholia"
     role="button"
     class="btn btn-outline-dark btn-sm"
     href="https://scholia.toolforge.org/${ p }">Scholia ↗</a>

  <hr/>

  <h2>Lexemes</h2>

  <h3 id="Lexemes-by-value">Lexemes by value</h3>

  <table class="table table-hover" id="values-for-lexemes-by-value"></table>

  <h3>Lexemes by language and value</h3>
  
  <table class="table table-hover" id="values-for-lexemes-by-language-and-value"></table>

  <hr/>
  
  <h2>Forms</h2>
  
  <table class="table table-hover" id="forms"></table>

  <hr/>
  
  <h2>Senses</h2>
  
  <table class="table table-hover" id="senses"></table>
  
</div>
