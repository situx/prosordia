---
layout: base
---

<script type="text/javascript">
 formDataSparql = `
# Data for a specific sense
SELECT
  ?description ?descriptionUrl
  (?valueLabel AS ?value_) ?value_Url
  ?source ?sourceUrl
WHERE {
  VALUES ?form { wd:${ l }-${ f } }
  {
    BIND(1 AS ?order)
    ?form ontolex:representation ?value .
    BIND(?value AS ?valueLabel)
    BIND("representation" AS ?description)
    BIND(CONCAT("representation/", ENCODE_FOR_URI(?value)) AS ?value_Url)
  }
  UNION
  {
    BIND(2 AS ?order)
    ?value ontolex:lexicalForm ?form .
    ?value wikibase:lemma ?valueLabel .
    BIND(SUBSTR(STR(?value), 32)  AS ?value_Url)
    BIND("lexeme" AS ?description)
  }
  UNION
  {
    # Language	
    BIND(3 AS ?order)
    ?form ^ontolex:lexicalForm / dct:language ?value .
    ?value rdfs:label ?valueLabel .
    FILTER (LANG(?valueLabel) = "en")
    BIND(CONCAT("language/", SUBSTR(STR(?value), 32))  AS ?value_Url)
    BIND("language" AS ?description)
  }
  UNION 
  {
    BIND(4 AS ?order)
    BIND("Instance of" AS ?description)
    ?form p:P31 ?value_statement .
    ?value_statement ps:P31 ?value .
    ?value rdfs:label ?valueLabel .
    FILTER (LANG(?valueLabel) = 'en')
    BIND(CONCAT("property_value?p=P31&q=", SUBSTR(STR(?value), 32))  AS ?value_Url)
    OPTIONAL {
      ?value_statement prov:wasDerivedFrom / pr:P248 ?source_ .
      ?source_ rdfs:label ?source .
      FILTER (LANG(?source) = 'en')
      BIND(CONCAT("reference/", SUBSTR(STR(?source_), 32))  AS ?sourceUrl)
    }
  }
  UNION 
  {
    # Characterized by
    BIND(5 AS ?order)
    BIND("characterized by" AS ?description)
    ?form wdt:P1552 ?value .
    ?value rdfs:label ?valueLabel .
    FILTER (LANG(?valueLabel) = "en")
    BIND(CONCAT("property_value?p=P1552&q=", SUBSTR(STR(?value), 32))  AS ?value_Url)
  }
  UNION
  {
    # Hyphenation
    BIND(10 AS ?order)
    ?form wdt:P5279 ?value .
    BIND(?value AS ?valueLabel)
    BIND("hyphenation" AS ?description)
  }
  UNION  
  {   
    BIND(11 AS ?order)
    ?form wdt:P2859 ?value .
    BIND(?value AS ?valueLabel)
    BIND("X-SAMPA" AS ?description)
  }
  UNION
  {
    # Alternative form
    BIND(12 AS ?order)
    ?form wdt:P8530 ?value .
    ?value ontolex:representation ?valueLabel .
    BIND(SUBSTR(STR(?value), 32)  AS ?value_Url)
    BIND("alternative form" AS ?description)
  }
  UNION
  {
    # Homofon form
    BIND(13 AS ?order)

    BIND("homophone form" AS ?description)
    BIND("../property?p=P10822" AS ?descriptionUrl)

    ?form wdt:P10822 ?value .
    ?value ontolex:representation ?valueLabel .
  }
  UNION 
  {
    BIND(20 AS ?order)

    BIND("Described in" AS ?description)
    BIND("../property?=p=P1343" AS ?descriptionUrl)

    ?form p:P1343 ?value_statement .
    ?value_statement ps:P1343 ?value .
    ?value rdfs:label ?valueLabel .
    FILTER (LANG(?valueLabel) = 'en')
    BIND(CONCAT("reference/", SUBSTR(STR(?value), 32))  AS ?value_Url)
    OPTIONAL {
      ?value_statement prov:wasDerivedFrom / pr:P854 ?sourceUrl .
      BIND("↗" AS ?source)
    }
  }
  UNION 
  {
    BIND(21 AS ?order)

    BIND("Not described in" AS ?description)
    BIND("../property/P9660" AS ?descriptionUrl)

    ?form p:P9660 ?value_statement .
    ?value_statement ps:P9660 ?value .
    ?value rdfs:label ?valueLabel .
    FILTER (LANG(?valueLabel) = 'en')
    BIND(CONCAT("reference/", SUBSTR(STR(?value), 32))  AS ?value_Url)
    OPTIONAL {
      ?value_statement prov:wasDerivedFrom / pr:P854 ?sourceUrl .
      BIND("↗" AS ?source)
    }
  }

  UNION 
  {
    BIND(25 AS ?order)
    BIND("Attested in" AS ?description)
    ?form p:P5323 ?value_statement .
    ?value_statement ps:P5323 ?value_ .

    OPTIONAL {
      ?value_ rdfs:label ?value_label .
      FILTER (LANG(?value_label) = "en")
    }
    # If English label are missing
    BIND(COALESCE(?value_label, SUBSTR(STR(?value_), 32)) AS ?value) 


    BIND(CONCAT("reference/", SUBSTR(STR(?value_), 32))  AS ?value_Url)
    OPTIONAL {
      ?value_statement prov:wasDerivedFrom / pr:P854 ?sourceUrl .
      OPTIONAL { ?value_statement pq:P577 ?date }
      BIND(COALESCE(
	  CONCAT(STR(YEAR(?date)), "-", STR(MONTH(?date)), "-", STR(DAY(?date)), "&nbsp;↗"),
        "↗") AS ?source)
    }
  }
  UNION  
  {   
    BIND(26 AS ?order)
    ?lexeme ontolex:lexicalForm ?form .
    ?lexeme p:P5831 ?value_statement .
    ?value_statement ps:P5831 ?value .
    ?value_statement pq:P5830 ?form .
    ?lexeme dct:language / wdt:P424 ?language_code .
    BIND(CONCAT("./text-to-lexemes?text-language=",
		ENCODE_FOR_URI(?language_code),
		"&text=",
		ENCODE_FOR_URI(?value)) AS ?value_Url)
    BIND("example use" AS ?description)
  }
}
ORDER BY ?order
`

 homographsSparql = `
# Homographs for a lexeme form
SELECT
  ?lexeme (SUBSTR(STR(?lexeme), 32) AS ?lexemeLabel)
  ?lemma ?lexical_category ?lexical_categoryLabel
  ?form (SUBSTR(STR(?form), 32) AS ?formLabel)
  (GROUP_CONCAT(DISTINCT ?featureLabel; separator=" // ") AS ?features)
WHERE {
  wd:${ l }-${ f } ontolex:representation / ^ontolex:representation ?form .

  FILTER (wd:${ l }-${ f } != ?form)

  ?lexeme ontolex:lexicalForm ?form ;
          wikibase:lemma ?lemma ;
          wikibase:lexicalCategory ?lexical_category .
  OPTIONAL {
    ?form wikibase:grammaticalFeature ?feature .
    ?feature rdfs:label ?featureLabel .
    FILTER (LANG(?featureLabel) = "en")
  }
    
  SERVICE wikibase:label { bd:serviceParam wikibase:language "[AUTO_LANGUAGE],en". }
  }
GROUP BY ?lexeme ?lemma ?lexical_category ?lexical_categoryLabel
  ?form ?formLabel
`

 wikisourceSparql = `
 SELECT ?title ?titleUrl ?snippet WHERE {
  SERVICE wikibase:mwapi {
      bd:serviceParam wikibase:api "Search" .
      bd:serviceParam wikibase:endpoint "{{ iso639 }}.wikisource.org" .
      bd:serviceParam mwapi:srsearch "\\"{{ representation }}\\"" .
      bd:serviceParam mwapi:language "{{ iso639 }}" .
      ?title wikibase:apiOutput mwapi:title .
      ?snippet_ wikibase:apiOutput "@snippet" .
  }
  hint:Prior hint:runFirst "true" .
  BIND(CONCAT("https://{{ iso639 }}.wikisource.org/wiki/", ENCODE_FOR_URI(?title)) AS ?titleUrl)
  BIND(REPLACE(REPLACE(?snippet_, '</span>', ''), '<span class="searchmatch">', '') AS ?snippet)
}
LIMIT 50
`

 wiktionarySparql = `
 SELECT ?title ?titleUrl ?snippet WHERE {
  SERVICE wikibase:mwapi {
      bd:serviceParam wikibase:api "Search" .
      bd:serviceParam wikibase:endpoint "{{ iso639 }}.wiktionary.org" .
      bd:serviceParam mwapi:srsearch "\\"{{ representation }}\\"" .
      bd:serviceParam mwapi:language "{{ iso639 }}" .
      ?title wikibase:apiOutput mwapi:title .
      ?snippet_ wikibase:apiOutput "@snippet" .
  }
  hint:Prior hint:runFirst "true" .
  BIND(CONCAT("https://{{ iso639 }}.wiktionary.org/wiki/", ENCODE_FOR_URI(?title)) AS ?titleUrl)
  BIND(REPLACE(REPLACE(?snippet_, '</span>', ''), '<span class="searchmatch">', '') AS ?snippet)
}
LIMIT 200
`

 wikiquoteSparql = `
 SELECT ?title ?titleUrl ?snippet WHERE {
  SERVICE wikibase:mwapi {
      bd:serviceParam wikibase:api "Search" .
      bd:serviceParam wikibase:endpoint "{{ iso639 }}.wikiquote.org" .
      bd:serviceParam mwapi:srsearch "\\"{{ representation }}\\"" .
      bd:serviceParam mwapi:language "{{ iso639 }}" .
      ?title wikibase:apiOutput mwapi:title .
      ?snippet_ wikibase:apiOutput "@snippet" .
  }
  hint:Prior hint:runFirst "true" .
  BIND(CONCAT("https://{{ iso639 }}.wikiquote.org/wiki/", ENCODE_FOR_URI(?title)) AS ?titleUrl)
  BIND(REPLACE(REPLACE(?snippet_, '</span>', ''), '<span class="searchmatch">', '') AS ?snippet)
}
LIMIT 50
`
   

 $(document).ready(function() {
     sparqlToDataTable(
	 formDataSparql, "#form-data",
	 options={
	     paging: false,
		 linkPrefixes: {
			value: '../representation'
		 }
	 });
     sparqlToDataTable(
	 homographsSparql, "#homographs",options={
		 linkPrefixes: {
			lexeme: '../l',
			form: '../lf'
		 }
	 });

     sparqlToDataTable(
	 wikisourceSparql, "#wikisource-table");

     sparqlToDataTable(
	 wiktionarySparql, "#wiktionary-table");

     sparqlToDataTable(
	 wikiquoteSparql, "#wikiquote-table");
 });

</script>

<div class="l-entity">
    <h1>Form: ${ l }-${ f }</h1>

    <a title="Back"
       role="button"
       class="btn btn-outline-dark btn-sm"
       href="../l?l=${ l }">←</a>
    <a title="View and edit form in Wikidata"
       role="button"
       class="btn btn-outline-dark btn-sm"
       href="https://www.wikidata.org/wiki/Lexeme:${ l }#${ f }">Wikidata ↗</a>

    
    <table class="table table-hover" id="form-data"></table>


    <h2>Homographs</h2>
    
    <table class="table table-hover" id="homographs"></table>

    <h2>Wikisource</h2>
    
    <table class="table table-hover" id="wikisource-table"></table>

    <h2>Wiktionary</h2>
    
    <table class="table table-hover" id="wiktionary-table"></table>

    <h2>Wikiquote</h2>
    
    <table class="table table-hover" id="wikiquote-table"></table>

  
  
</div>



